use re
use str
use path
use ./core
use ./console
use ./project

fn detect {
  var head-ref = $E:GITHUB_HEAD_REF
  var ref = $E:GITHUB_REF

  var retrieved-branch = (
    core:ternary (core:not-empty $head-ref) $head-ref $ref
  )

  if (core:empty $retrieved-branch) {
    fail "Cannot retrieve the branch!"
  }

  console:inspect &emoji=ğŸŒ³ "Retrieved Git branch name" $retrieved-branch

  var branch = (path:base $retrieved-branch)
  console:inspect &emoji=ğŸŒ² "Current Git branch" $branch

  var version = (str:trim-prefix $branch v)
  console:inspect &emoji=ğŸ¦‹ "Detected version" $version

  var escaped-version = (re:quote $version)
  console:inspect &emoji=ğŸ§µ "Escaped version" $escaped-version

  var major = (
    put $version |
      str:split . (all) |
      take 1 |
      str:split - (all) |
      take 1 |
      str:split + (all) |
      take 1
  )
  console:inspect &emoji=ğŸª© "Major version" $major
  put [
    &branch=$branch
    &version=$version
    &escaped-version=$escaped-version
    &major=$major
  ]
}

fn -inject { |project|
  var branch-version = (detect)[version]

  core:edit-file $project[descriptor-path] { |text|
    str:replace '0.0.0' $branch-version $text
  }

  echo âœ…Version injected!

  $project[print-descriptor]
}

fn -check { |project|
  $project[print-descriptor]

  var branch-version = (detect)[version]
  console:inspect &emoji=ğŸŒ² 'Branch version' $branch-version

  var project-version = ($project[read-version])

  if (is $project-version $nil) {
    console:print "ğŸ·The project version cannot be detected"
    echo ğŸ”Ensuring the branch version exists in the project...

    var descriptor-content = (slurp < $project[descriptor-path])

    if (str:contains $descriptor-content $branch-version) {
      echo âœ…Branch version found in the descriptor!
    } else {
      fail 'The branch version cannot be found in the artifact descriptor!'
    }
  } else {
    console:inspect &emoji=ğŸ· 'Project version' $project-version

    if (==s $project-version $branch-version) {
      echo âœ…The project version matches the branch version!
    } else {
      fail 'The project version and the branch version do not match!'
    }
  }
}

fn enforce { |project-directory mode &descriptor-name=$nil|
  var strategies = [
    &inject={
      var project = (project:detect &descriptor-name=$descriptor-name $project-directory)
      echo ğŸ§¬Injecting branch version into project: ($project[to-string])
      -inject $project
    }

    &check={
      var project = (project:detect &descriptor-name=$descriptor-name $project-directory)
      echo ğŸ”Checking branch version for project: ($project[to-string])
      -check $project
    }

    &skip={
      echo ğŸ’­Skipping branch version enforcement, as requested...
    }
  ]

  if (not (has-key $strategies $mode)) {
    fail "Invalid mode: '"$mode"'"
  }

  $strategies[$mode]
}
